plugins {
    id 'groovy'
    id 'codenarc'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.14.0'
}

sourceCompatibility = 1.8

dependencies {
    implementation gradleApi()
    implementation 'net.sf.saxon:Saxon-HE:10.5'
    implementation 'org.xmlresolver:xmlresolver:2.0.0'

    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }

    testImplementation gradleTestKit()
}

repositories {
    mavenCentral()
}

group = 'com.nwalsh'
version = '0.9.1-beta2'

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

wrapper {
    gradleVersion = '7.0'
}

codenarc {
    toolVersion = '2.1.0'
    configFile = file('config/codenarc/codenarc.groovy')
}

pluginBundle {
    website = 'https://github.com/ndw/saxon-gradle'
    vcsUrl = 'https://github.com/ndw/saxon-gradle.git'
    description = 'A Gradle plugin for running Saxon'
    tags = ['saxon', 'xslt', 'documentation']
}

gradlePlugin {
    plugins {
        SaxonPlugin {
            id = 'com.nwalsh.saxon-gradle'
            displayName = 'Saxon Gradle Plugin'
            implementationClass = 'com.nwalsh.SaxonPlugin'
            description = 'A Gradle plugin for running XSLT transformations with Saxon'
        }
    }
}

// I can't figure out how to get test to pass...
//publishPlugins.dependsOn check
