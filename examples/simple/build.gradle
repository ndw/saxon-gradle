buildscript {
  repositories {
    mavenCentral()
    maven { url "https://dev.saxonica.com/maven" }
  }

  // The XQuery task requires Saxon 12.x (or 11.6 or later when
  // that is available.)
  dependencies {
    classpath group: 'net.sf.saxon', name: 'Saxon-HE', version: '12.1'
  }
}

plugins {
  // This is a dummy version number â€” use the actual latest version number
  // instead.
  id 'com.nwalsh.gradle.saxon.saxon-gradle' version '1.0-SNAPSHOT'
}

import com.nwalsh.gradle.saxon.SaxonXsltTask
import com.nwalsh.gradle.saxon.SaxonXQueryTask

wrapper {
  gradleVersion = '8.0'
}

defaultTasks = ["xslt", "xslt_with_options", "xquery", "xquery_with_options"]

def xsltOptions = [
  'debug': true,
  'parameters': [
    "title": "spoon",
    "padding": "1em"
  ]]

def queryOptions = [
  'debug': true,
  'query': 'tools/query.xq',
  'parameters': [
    'multiplier': 7
  ]]
  
task xslt(type: SaxonXsltTask) {
  // Use URIs, just to show a simple arg
  stylesheet file("${projectDir}/tools/html5.xsl")
  input "file://${projectDir}/xml/input-1.xml"
  output "file://${buildDir}/output-1.xml"
  arg '-u'
  parameters([
    "title": "spoon",
    "padding": "1em"
  ])
}

task xslt_with_options(type: SaxonXsltTask) {
  stylesheet file("${projectDir}/tools/html5.xsl")
  input "file://${projectDir}/xml/input-1.xml"
  output "file://${buildDir}/output-1.xml"
  options(xsltOptions)
}

if ("yes".equals(System.getenv("CIWORKFLOW"))
    && System.getProperty("os.name").startsWith("Windows")) {
  task xquery() {
    println("The xquery tasks don't run in CI under Windows")
    println("For inexplicable reasons, the paths get mangled.")
  }
} else {
  task xquery(type: SaxonXQueryTask) {
    query 'tools/query.xq'
    input 'xml/input-1.xml'
    output "${buildDir}/output-2.xml"
    parameters([
      'multiplier': 7
    ])
  }

  task xquery_with_options(type: SaxonXQueryTask) {
    input 'xml/input-1.xml'
    output "${buildDir}/output-2.xml"
    options queryOptions
  }
}
