buildscript {
  repositories {
    mavenCentral()
    maven { url "https://dev.saxonica.com/maven" }
  }

  // The XQuery task requires Saxon 12.x (or 11.6 or later when
  // that is available.)
  dependencies {
    classpath group: 'net.sf.saxon', name: 'Saxon-HE', version: '12.1'
  }
}

plugins {
  // This is a dummy version number â€” use the actual latest version number
  // instead.
  id 'com.nwalsh.gradle.saxon.saxon-gradle' version '1.0-SNAPSHOT'
}

import com.nwalsh.gradle.saxon.SaxonXsltTask
import com.nwalsh.gradle.saxon.SaxonXQueryTask

wrapper {
  gradleVersion = '8.0'
}

defaultTasks = ["xslt", "xquery"]

task xslt(type: SaxonXsltTask) {
  debug true
  xmlDepends true
  xslDepends true
  stylesheet 'tools/html5.xsl'
  input 'xml/input-1.xml'
  output 'build/output-1.xml'
  parameters([
    "title": "spoon",
    "padding": "1em"
  ])
}

if ("yes".equals(System.getenv("CIWORKFLOW"))
    && System.getProperty("os.name").startsWith("Windows")) {
  task xquery() {
    println("The xquery tasks don't run in CI under Windows")
    println("For inexplicable reasons, the paths get mangled.")
  }
} else {
  task xquery(type: SaxonXQueryTask) {
    debug true
    xmlDepends true
    query 'tools/query.xq'
    input 'xml/input-1.xml'
    output "${buildDir}/output-2.xml"
    parameters([
      'multiplier': 7
    ])
  }
}
